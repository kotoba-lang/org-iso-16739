(ns ifc.mvd-test
  (:require [clojure.string :as string]
            [clojure.test :refer [deftest is testing]]
            [ifc.core :as ifc]
            [ifc.mvd :as mvd]))

(deftest detects-and-validates-model-view-declarations
  (is (= :reference-view
         (mvd/detect-profile ["ViewDefinition [Reference View] no scale"])))
  (is (= :alignment-based-reference-view
         (mvd/detect-profile ["ViewDefinition [Alignment-based Reference View]"])))
  (is (:mvd/pass?
       (mvd/validate-declaration
        {:schema "IFC4" :profile :reference-view
         :descriptions ["ViewDefinition [ReferenceView]"]})))
  (is (false?
       (:mvd/pass?
        (mvd/validate-declaration
         {:schema "IFC2X3" :profile :reference-view
          :descriptions ["ViewDefinition [ReferenceView]"]}))))
  (testing "incompatible output declarations fail before serialization"
    (is (thrown? #?(:clj Exception :cljs js/Error)
                 (mvd/header-description "IFC2X3" :design-transfer-view)))))

(deftest writes-and-reads-the-selected-model-view
  (let [document (assoc (ifc/exchange-document
                         {:project {:global-id "1hqIFTRjfV6AWq_bMtnZwI"
                                    :name "Reference model"}
                          :elements []})
                        :ifc/model-view :reference-view)
        output (ifc/write-spf document)
        imported (ifc/read-document output)]
    (is (string/includes? output "ViewDefinition [ReferenceView]"))
    (is (= :reference-view (:ifc/model-view imported)))
    (is (= ["ViewDefinition [ReferenceView]"]
           (get-in imported [:ifc/header :description])))))
